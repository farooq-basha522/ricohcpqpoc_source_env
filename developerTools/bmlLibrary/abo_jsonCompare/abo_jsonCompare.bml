// Function Name : abo_jsonCompare
//   Inputs: jsonObj1(Json), jsonObj2(Json), jsonCritArray(JsonArray)
//   output: 0 if both objects are equal as per jsonCrit's in jsonCritArray.
//          -1 if it satisfies the order as per jsonCrit's in jsonCritArray.
//           1 if it does not satisfy the order as per jsonCrit's in jsonCritArray.
// Description:
//    This utility BML is a supporting function for abo_jsonSort, 
//    it compares two homogeneous json object based on the criteria specified.
//
// The input of jsonObj1 and jsonObj2 has to be homogeneous, 
// the input of  jsonCritArray is a list of jsonCrit objects. 
//    see abo_jsonSort function for details & example

dummyDateStr = "1000/01/01 00:00:00";
dummyDate = strtojavadate(dummyDateStr , "yyyy/MM/dd HH:mm:ss");
dummyNumber = "-999999999999999999";

critArraySize = jsonarraysize(jsonCritArray);
index = -1;
critIterator = string[critArraySize];

returnInt = 0;  //default result if no crit defined or if the user has a typo in crit.
                
for i in critIterator{
    returnInt = 1;  
    index = index+1;
    
    //retrieving the criteria
        jsonCrit = jsonarrayget(jsonCritArray, index, "json");
    order = jsonget(jsonCrit, "order", "string", "asc");
    type = jsonget(jsonCrit , "type", "string", "string");
    field= jsonget(jsonCrit , "field");
    dateFormat = jsonget(jsonCrit, "dateFormat", "string", "yyyy/MM/dd HH:mm:ss");
    
    //retrieve the field value
    string1 = jsonget(jsonObj1 , field);
    string2 = jsonget(jsonObj2 , field);
    
    //if value is empty, default to empty value.
    if(isnull(string1) or trim(string1)== ""){  
      if(type=="number"){
        string1 = dummyNumber;
      }
      if(type=="date"){    
        string1 = datetostr(dummyDate, dateFormat ); 
      }       
    }
    
    if(isnull(string2) or trim(string2)== ""){
      if(type=="number"){
        string2 = dummyNumber ;
      }
      if(type == "date"){
        string2 = datetostr(dummyDate, dateFormat );
      }
      
    }
    
    //now we will do real comparison, we have one block for each type supported.
    if(type == "string"){
      if(string1 == string2){
        returnInt = 0;
       }elif(order == "asc" and string1 < string2){
        returnInt = -1;
      }elif(order == "desc" and string1 > string2){
       returnInt = -1;
      }
    }
    elif(type == "number"){
      if(atof(string1) == atof(string2)){
        returnInt = 0;
      }elif(order == "asc" and atof(string1) < atof(string2)){
        returnInt =-1;
      }elif(order == "desc" and atof(string1) > atof(string2)){
        returnInt = -1;
      }
    }
    
    elif(type == "date"){
      date1 = strtojavadate(string1, dateFormat );
      date2 = strtojavadate(string2, dateFormat );      
      if(comparedates(date1, date2) == 0){
         returnInt = 0;
      }
      elif(order == "asc" and comparedates(date1, date2) < 0){
        returnInt = -1;
      }elif(order == "desc" and comparedates(date1, date2) > 0){
        returnInt = -1;
      }
      
    }
    
    //now we have the final result of current crit
    if(returnInt == 0){
      continue; // it is a tie, so we will have to evaluate next crit
    }else{
      break;
    }
    
} //end of loop through critArray

return returnInt;